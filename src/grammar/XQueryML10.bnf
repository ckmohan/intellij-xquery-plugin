{
  classHeader="header.txt"
  parserClass="uk.co.overstory.parser.GrammarParser"
  stubParserClass="org.intellij.grammar.parser.GrammarParserUtil"

  implements="uk.co.overstory.xquery.psi.XqyCompositeElement"
  extends="uk.co.overstory.xquery.psi.impl.XqyCompositeElementImpl"

  psiClassPrefix="Xqy"
  psiImplClassSuffix="Impl"
  psiPackage="uk.co.overstory.xquery.psi"
  psiImplPackage="uk.co.overstory.xquery.psi.impl"

  elementTypeHolderClass="uk.co.overstory.xquery.psi.XqyTypes"
  elementTypePrefix="XQY_"
  elementTypeClass="uk.co.overstory.xquery.psi.XqyCompositeElementType"
  tokenTypeClass="uk.co.overstory.xquery.psi.XqyTokenType"

  // tokens
  CDATA_START="<![CDATA["
  CDATA_END="]>"
  OP_BIND=":="
  EQUAL="="
  NOT_EQUAL="!="
  LESS_EQUAL="<="
  GREATER_EQUAL=">="
  DOC_BEFORE="<<"
  DOC_AFTER=">>"
  VERT_BAR="|"
  QMARK="?"
  PLUS_SIGN="+"
  MINUS_SIGN="-"
  STAR="*"
  AT_SIGN="@"
  HASH="#"
  SEMICOLON=";"
  COLON=":"
  UNDERSCORE="_"
  LEFT_PAREN="("
  RIGHT_PAREN=")"
  LEFT_BRACE="{"
  RIGHT_BRACE="}"
  LEFT_BRACKET="["
  RIGHT_BRACKET="]"
  LT="<"
  GT=">"
  END_TAG_START="</"
  EMPTY_TAG_END="/>"
  DOLLAR="$"
  AMP="&"
  COMMA=","
  DOT="."
  DOTDOT=".."
  AXIS="::"
  SLASH="/"
  SLASH_SLASH="//"
  PRAGMA_START="(#"
  PRAGMA_END="#)"
  DBL_LEFT_BRACE="{{"
  DBL_RIGHT_BRACE="}}"
  XML_COMMENT_START="<!--"
  XML_COMMENT_END="-->"
  PI_START="<?"
  PI_END="?>"
  CHAR_REF_START="&#"
  CHAR_HEX_REF_START="&#x"

  implements("QName|NCName")="uk.co.overstory.xquery.psi.XqyNamedElement"
  extends("QName|NCName")="uk.co.overstory.xquery.psi.impl.XqyNamedElementImpl"
  mixin("RefVarName")="uk.co.overstory.xquery.psi.impl.XqyRefVarName"
/*
  mixin("StringLiteral")="uk.co.overstory.xquery.psi.impl.XqyStringImpl"
  implements("Predicate|EnclosedExpr|ParenthesizedExpr|Comment")="uk.co.overstory.xquery.psi.XqyParenthesized"

*/
}


XQuery   ::= Module EOF
Module   ::= VersionDecl? ( LibraryModule | MainModuleSequence )
VersionDecl ::= 'xquery' 'version' XqueryVersionString ( 'encoding' XqueryEncodingString )? Separator
XqueryEncodingString ::= StringLiteral
XqueryVersionString ::= StringLiteral

MainModuleSequence ::= MainModule ( ';' VersionDecl? MainModule)* ';'?

MainModule ::= Prolog QueryBody
LibraryModule ::= ModuleDecl Prolog

ModuleDecl ::= 'module' 'namespace' NamespaceName '=' NamespaceValue Separator

Prolog   ::= ( ( DefaultNamespaceDecl | Setter | NamespaceDecl | Import ) Separator )* ( ( VarDecl | FunctionDecl | OptionDecl ) Separator )*
Setter   ::= BoundarySpaceDecl
           | DefaultCollationDecl
           | BaseURIDecl
           | ConstructionDecl
           | OrderingModeDecl
           | EmptyOrderDecl
           | CopyNamespacesDecl
Import   ::= SchemaImport | ModuleImport | ImportStylesheetDecl
Separator ::= ';'

NamespaceDecl ::= 'declare' 'namespace' NamespaceName '=' NamespaceValue
NamespaceName ::= NCName
NamespaceValue ::= URILiteral

PathHint ::= StringLiteral

BoundarySpaceDecl ::= 'declare' 'boundary-space' BoundarySpaceAction
BoundarySpaceAction ::= ( 'preserve' | 'strip' )

DefaultNamespaceDecl ::= 'declare' 'default' DefaultNamespaceKind 'namespace' NamespaceValue
DefaultNamespaceKind ::= ( 'element' | 'function' )

ImportStylesheetDecl ::= 'import' 'stylesheet' 'at' PathHint

OptionDecl ::= 'declare' 'option' OptionName StringLiteral
OptionName ::= QName
OrderingModeDecl ::= 'declare' 'ordering' ( 'ordered' | 'unordered' )
EmptyOrderDecl ::= 'declare' 'default' 'order' 'empty' ( 'greatest' | 'least' )
CopyNamespacesDecl ::= 'declare' 'copy-namespaces' PreserveMode ',' InheritMode
PreserveMode ::= 'preserve' | 'no-preserve'
InheritMode ::= 'inherit' | 'no-inherit'
DefaultCollationDecl ::= 'declare' 'default' 'collation' URILiteral
BaseURIDecl ::= 'declare' 'base-uri' URILiteral
SchemaImport ::= 'import' 'schema' SchemaPrefix? URILiteral ( 'at' PathHint ( ',' PathHint )* )?
SchemaPrefix ::= 'namespace' NamespaceName '=' | 'default' 'element' 'namespace'
ModuleImport ::= 'import' 'module' ( 'namespace' NamespaceName '=' )? NamespaceValue ( 'at' PathHint ( ',' PathHint )* )?

VarDecl  ::= 'declare' 'private'? 'variable' '$' VarName TypeDeclaration? ( ':=' ExprSingle | 'external' )

ConstructionDecl ::= 'declare' 'construction' ( 'strip' | 'preserve' )
FunctionDecl ::= 'declare' 'private'? 'function' FunctionName '(' ParamList? ')' ( 'as' SequenceType )? ( EnclosedExpr | 'external' )

ParamList ::= Param ( ',' Param )*
Param    ::= '$' ParamName TypeDeclaration?
ParamName ::= QName

EnclosedExpr ::= '{' Expr '}'
QueryBody ::= Expr
Expr     ::= ExprSingle ( ',' ExprSingle )*
ExprSingle ::= FLWORExpr | QuantifiedExpr | TypeswitchExpr | IfExpr | OrExpr | TryCatchExpr

TryCatchExpr ::= 'try' EnclosedExpr 'catch' '(' '$' VarName ')' EnclosedExprExtended
EnclosedExprExtended ::= '{' Expr? '}'

FLWORExpr ::= ( ForClause | LetClause )+ WhereClause? OrderByClause? 'return' ExprSingle
ForClause ::= 'for' '$' VarName TypeDeclaration? PositionalVar? 'in' ExprSingle ( ',' '$' VarName TypeDeclaration? PositionalVar? 'in' ExprSingle )*
PositionalVar ::= 'at' '$' VarName
LetClause ::= 'let' '$' VarName TypeDeclaration? ':=' ExprSingle ( ',' '$' VarName TypeDeclaration? ':=' ExprSingle )*
WhereClause ::= 'where' ExprSingle
OrderByClause ::= ( 'order' 'by' | 'stable' 'order' 'by' ) OrderSpecList
OrderSpecList ::= OrderSpec ( ',' OrderSpec )*
OrderSpec ::= ExprSingle OrderModifier
OrderModifier ::= ( 'ascending' | 'descending' )? ( 'empty' ( 'greatest' | 'least' ) )? ( 'collation' URILiteral )?
QuantifiedExpr ::= ( 'some' | 'every' ) '$' VarName TypeDeclaration? 'in' ExprSingle ( ',' '$' VarName TypeDeclaration? 'in' ExprSingle )* 'satisfies' ExprSingle
TypeswitchExpr ::= 'typeswitch' '(' Expr ')' CaseClause+ 'default' ( '$' VarName )? 'return' ExprSingle
CaseClause ::= 'case' ( '$' VarName 'as' )? SequenceType 'return' ExprSingle
IfExpr   ::= 'if' '(' Expr ')' 'then' ExprSingle 'else' ExprSingle
OrExpr   ::= AndExpr ( 'or' AndExpr )*
AndExpr  ::= ComparisonExpr ( 'and' ComparisonExpr )*
ComparisonExpr ::= RangeExpr ( ( ValueComp | GeneralComp | NodeComp ) RangeExpr )?
RangeExpr ::= AdditiveExpr ( 'to' AdditiveExpr )?
AdditiveExpr ::= MultiplicativeExpr ( ( '+' | '-' ) MultiplicativeExpr )*
MultiplicativeExpr ::= UnionExpr ( ( '*' | 'div' | 'idiv' | 'mod' ) UnionExpr )*
UnionExpr ::= IntersectExceptExpr ( ( 'union' | '|' ) IntersectExceptExpr )*
IntersectExceptExpr ::= InstanceofExpr ( ( 'intersect' | 'except' ) InstanceofExpr )*
InstanceofExpr ::= TreatExpr ( 'instance' 'of' SequenceType )?
TreatExpr ::= CastableExpr ( 'treat' 'as' SequenceType )?
CastableExpr ::= CastExpr ( 'castable' 'as' SingleType )?
CastExpr ::= UnaryExpr ( 'cast' 'as' SingleType )?
UnaryExpr ::= ( '-' | '+' )* ValueExpr
ValueExpr ::= ValidateExpr | PathExpr | ExtensionExpr
GeneralComp ::= '=' | '!=' | '<' | '<=' | '>' | '>='
ValueComp ::= 'eq' | 'ne' | 'lt' | 'le' | 'gt' | 'ge'
NodeComp ::= 'is' | '<<' | '>>'
ValidateExpr ::= 'validate' ('as' QName | ValidationMode)? 'full'? '{' Expr '}'
ValidationMode ::= 'lax' | 'strict'
ExtensionExpr ::= Pragma+ '{' Expr? '}'
Pragma   ::= '(#' S? QName ( S PragmaContents )? '#)'
          /* ws: explicit */
PathExpr ::= '/' ( RelativePathExpr '/' ) | '//' RelativePathExpr | RelativePathExpr
RelativePathExpr ::= StepExpr ( ( '/' | '//' ) StepExpr )*
StepExpr ::= FilterExpr | AxisStep
AxisStep ::= ( ReverseStep | ForwardStep ) PredicateList
ForwardStep ::= ForwardAxis NodeTest | AbbrevForwardStep
ForwardAxis ::= 'child' '::'
           | 'descendant' '::'
           | 'attribute' '::'
           | 'namespace' '::'
           | 'self' '::'
           | 'property' '::'
           | 'descendant-or-self' '::'
           | 'following-sibling' '::'
           | 'following' '::'
AbbrevForwardStep ::= '@'? NodeTest
ReverseStep ::= ReverseAxis NodeTest | AbbrevReverseStep
ReverseAxis
         ::= 'parent' '::'
           | 'ancestor' '::'
           | 'preceding-sibling' '::'
           | 'preceding' '::'
           | 'ancestor-or-self' '::'

AbbrevReverseStep ::= '..'
NodeTest ::= KindTest | NameTest
NameTest ::= QName | Wildcard
FilterExpr ::= PrimaryExpr PredicateList
PredicateList ::= Predicate*
Predicate ::= '[' Expr ']'
PrimaryExpr ::= Literal
           | VarRef
           | ParenthesizedExpr
           | ContextItemExpr
           | FunctionCall
           | OrderedExpr
           | UnorderedExpr
           | Constructor

Literal  ::= NumericLiteral | StringLiteral
NumericLiteral ::= number

VarRef  ::= '$' RefVarName
RefVarName ::= QName
VarName  ::= QName
ParenthesizedExpr ::= '(' Expr? ')'
ContextItemExpr ::= '.'
OrderedExpr ::= 'ordered' '{' Expr '}'
UnorderedExpr ::= 'unordered' '{' Expr '}'
FunctionCall ::= FunctionName '(' ( ExprSingle ( ',' ExprSingle )* )? ')'
Constructor ::= DirectConstructor | ComputedConstructor
DirectConstructor ::= DirElemConstructor | DirCommentConstructor | DirPIConstructor
DirElemConstructor ::= '<' QName DirAttributeList ( '/>' | '>' DirElemContent* '</' QName S? '>' )
          /* ws: explicit */
DirAttributeList ::= ( S ( QName S? '=' S? DirAttributeValue )? )*
          /* ws: explicit */
DirAttributeValue ::= string

QuotAttrValueContent ::= QuotAttrContentChar | CommonContent
AposAttrValueContent ::= AposAttrContentChar | CommonContent
          /* ws: explicit */
DirElemContent ::= DirectConstructor | CDataSection | CommonContent | ElementContentChar
CommonContent ::= PredefinedEntityRef | CharRef | '{{' | '}}' | EnclosedExpr
DirCommentConstructor ::= '<!--' DirCommentContents '-->'
          /* ws: explicit */
DirPIConstructor ::= '<?' PITarget ( S DirPIContents )? '?>'
          /* ws: explicit */
ComputedConstructor
         ::= CompDocConstructor
           | CompElemConstructor
           | CompAttrConstructor
           | CompTextConstructor
           | CompCommentConstructor
           | CompPIConstructor
           | CompBinaryConstructor
           | CompNamespaceConstructor

CompBinaryConstructor ::= 'binary' EnclosedExprExtended
CompNamespaceConstructor ::= 'namespace' ( NamespaceName | '{' Expr '}' ) EnclosedExprExtended

CompDocConstructor ::= 'document' '{' Expr '}'
CompElemConstructor ::= 'element' ( ElementName | '{' Expr '}' ) '{' ContentExpr? '}'
ContentExpr ::= Expr
CompAttrConstructor ::= 'attribute' ( AttributeName | '{' Expr '}' ) '{' Expr? '}'
CompTextConstructor ::= 'text' EnclosedExprExtended
CompCommentConstructor ::= 'comment' '{' Expr '}'
CompPIConstructor ::= 'processing-instruction' ( PIName | '{' Expr '}' ) '{' Expr? '}'
SingleType ::= AtomicType OccurrenceIndicator?
//SingleType ::= AtomicType ('?'|'*'^OccurrenceIndicator)?
TypeDeclaration ::= 'as' SequenceType
SequenceType ::= 'empty-sequence' '(' ')' | ItemType OccurrenceIndicator?
OccurrenceIndicator ::= '?' | '*' | '+'
//OccurrenceIndicator ::= '?' | '*'^OccurrenceIndicator | '+'^OccurrenceIndicator
ItemType ::= KindTest | 'item' '(' ')' | AtomicType
AtomicType ::= QName
KindTest ::= DocumentTest
           | ElementTest
           | AttributeTest
           | SchemaElementTest
           | SchemaAttributeTest
           | PITest
           | CommentTest
           | TextTest
           | AnyKindTest
           | BinaryTest
BinaryTest ::= 'binary' '(' ')'
AnyKindTest ::= 'node' '(' ')'
DocumentTest ::= 'document-node' '(' ( ElementTest | SchemaElementTest )? ')'
TextTest ::= 'text' '(' ')'
CommentTest ::= 'comment' '(' ')'
PITest   ::= 'processing-instruction' '(' ( NCName | StringLiteral )? ')'
AttributeTest ::= 'attribute' '(' ( AttribNameOrWildcard ( ',' TypeName )? )? ')'
AttribNameOrWildcard ::= AttributeName | '*'
SchemaAttributeTest ::= 'schema-attribute' '(' AttributeDeclaration ')'
AttributeDeclaration ::= AttributeName
ElementTest ::= 'element' '(' ( ElementNameOrWildcard ( ',' TypeName '?'? )? )? ')'
ElementNameOrWildcard ::= ElementName | '*'
SchemaElementTest ::= 'schema-element' '(' ElementDeclaration ')'
ElementDeclaration ::= ElementName
AttributeName ::= QName
ElementName ::= QName
TypeName ::= QName
PIName ::= NCName
URILiteral ::= StringLiteral { extends="StringLiteral" }

Keyword ::= FunctionName | FunctionQname | OpNCName

FunctionName ::= QName

Whitespace ::= ( S | Comment )*


//<?TOKENS?>

DirCommentContents ::= ( ( NotDash ) | '-' ( NotDash ) )*
          /* ws: explicit */

StringLiteral ::= string

PredefinedEntityRef ::= '&' AsciiAlpha ';'
          /* ws: explicit */
CharRef  ::= '&#' Digits ';' | '&#x' (Digits | Hex)+ ';'

NCName ::= id

QName    ::= PrefixedName | UnprefixedName
PrefixedName ::= Prefix ':' LocalPart
UnprefixedName ::= LocalPart
Prefix   ::= NCName
LocalPart ::= NCName
PITarget ::= NCName
PragmaContents ::= ( !'#' | '#'+ !'#' )* '#'* & '#'
DirPIContents ::= ( !'?' | '?'+ ( !'?' | !'>' ) )* '?'* & '?'
CDataSection ::= '<![CDATA[' ( !']' | ']' !']' | ']' ']'+ !']' )* ']'+ ']>'
Wildcard ::= NCName ':' '*' | '*' ( ':' NCName )?
EOF      ::= 0x0000

